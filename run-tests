#!/usr/bin/env nu

let OUTPUT_DIR = '/tmp/pm-tests'
let STDOUT_FILE = $'($OUTPUT_DIR)/stdout.txt'
let STDERR_FILE = $'($OUTPUT_DIR)/stderr.txt'

# TODO: It would be nice to keep all the output and error capturing in ths file. However, when there
# is an error in tests.nu, the following hangs:
# tmux new-session $'do -c { nu tests.nu | save ($STDOUT_FILE) } | to text | save ($STDERR_FILE)'

setup
tmux new-session -d nu tests.nu $STDOUT_FILE $STDERR_FILE
wait-until { ($STDOUT_FILE | path exists) ||($STDERR_FILE | path exists) }
report
teardown


def wait-until [cond: block, retry_interval: duration = 500ms, timeout: duration = 3sec] {
    if not (do $cond) {
        sleep $retry_interval
        if $timeout <= 0sec {
            false
        } else {
            wait-until $cond $retry_interval ($timeout - $retry_interval)
        }
    }
}

def report [] {
    if ($STDOUT_FILE | path exists) {
        cat $STDOUT_FILE
    } else {
        echo 'Warning: stdout file was not created'
    }
    if ($STDERR_FILE | path exists) {
        echo "\n-------------------------------------------------------------------------------------"
        cat $STDERR_FILE
    }
}

def setup [] {
    mkdir $OUTPUT_DIR
    rm -f $STDOUT_FILE
    rm -f $STDERR_FILE
    activate-test-projects
}

def teardown [] {
    activate-real-projects
}

def activate-test-projects [] {
    ^ln -sf ~/.pm/pm-test.yml ~/.pm.yml
}

def activate-real-projects [] {
    ^ln -sf ~/.pm/pm.yml ~/.pm.yml
}

def 'pm toggle-symlink' [] {
    let current_target = (
        ls -ld '~/.pm*'
            | where name =~ '.+/\.pm.yml'
            | get target
            | path basename
    ).0
    if $current_target == 'pm.yml' {
        activate-test-projects
    } else if $current_target == 'pm-test.yml' {
        activate-real-projects
    } else {
        error make {msg: $'Unexpected symlink target: ($current_target)'}
    }
    ls -ld ~/.pm.yml | select name target
}
